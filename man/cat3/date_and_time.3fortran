NAME

DATE_AND_TIME(3) - [SYSTEM:TIME] gets current time

SYNTAX

        subroutine date_and_time(date, time, zone, values)

         character(len=8),intent(out),optional :: date
         character(len=10),intent(out),optional :: time
         character(len=5),intent(out),optional :: zone
         integer,intent(out),optional :: values(8)

DESCRIPTION

DATE_AND_TIME(DATE, TIME, ZONE, VALUES) gets the corresponding date and
time information from the real-time system clock.

Unavailable time and date _character_ parameters return blanks.

ARGUMENTS

    DATE
        The type shall be _character(len=8)_ or larger, and of default
        kind. DATE has the form ccyymmdd.

    TIME
        The type shall be _character(len=10)_ or larger, and of default
        kind. TIME has the form hhmmss.sss.

    ZONE
        The type shall be _character(len=5)_ or larger, and of default
        kind. ZONE has form (+-)hhmm, representing the difference with
        respect to Coordinated Universal Time (UTC).

    VALUES
        An _integer_ array of eight elements. On return VALUES contains:

    - VALUES(1)
        The year
        -   VALUES(2)

        The month
        -   VALUES(3)

        The day of the month
        -   VALUES(4)

        Time difference with UTC in minutes
        -   VALUES(5)

        The hour of the day
        -   VALUES(6)

        The minutes of the hour
        -   VALUES(7)

        The seconds of the minute
        -   VALUES(8)

        The milliseconds of the second

EXAMPLES

Sample program:

    program demo_time_and_date
    implicit none
    character(len=8)     :: date
    character(len=10)    :: time
    character(len=5)     :: zone
    integer,dimension(8) :: values

        call date_and_time(date,time,zone,values)

        ! using keyword arguments
        call date_and_time(DATE=date,TIME=time,ZONE=zone)
        print '(*(g0))','DATE="',date,'" TIME="',time,'" ZONE="',zone,'"'

        call date_and_time(VALUES=values)
        write(*,'(i5,a)') &
         & values(1),' - The year', &
         & values(2),' - The month', &
         & values(3),' - The day of the month', &
         & values(4),' - Time difference with UTC in minutes', &
         & values(5),' - The hour of the day', &
         & values(6),' - The minutes of the hour', &
         & values(7),' - The seconds of the minute', &
         & values(8),' - The milliseconds of the second'
    end program demo_time_and_date

Results:

       DATE="20201222" TIME="165738.779" ZONE="-0500"
        2020 - The year
          12 - The month
          22 - The day of the month
        -300 - Time difference with UTC in minutes
          16 - The hour of the day
          57 - The minutes of the hour
          38 - The seconds of the minute
         779 - The milliseconds of the second

STANDARD

Fortran 95 and later

SEE ALSO

CPU_TIME(3), SYSTEM_CLOCK(3)

RESOURCES

date and time conversion, formatting and computation

-   M_time
-   datetime
-   datetime-fortran

fortran-lang intrinsic descriptions (@urbanjost)
