NAME

CSHIFT(3) - [TRANSFORMATIONAL] Circular shift elements of an array
(GFDL)

SYNTAX

    result = cshift(array, shift, dim)

DESCRIPTION

CSHIFT(ARRAY, SHIFT [, DIM]) performs a circular shift on elements of
ARRAY along the dimension of DIM. If DIM is omitted it is taken to be 1.
DIM is a scalar of type _integer_ in the range of 1 <= DIM <= N, where
"n" is the rank of ARRAY. If the rank of ARRAY is one, then all elements
of ARRAY are shifted by SHIFT places. If rank is greater than one, then
all complete rank one sections of ARRAY along the given dimension are
shifted. Elements shifted out one end of each rank one section are
shifted back in the other end.

ARGUMENTS

    ARRAY
        Shall be an array of any type.

    SHIFT
        The type shall be _integer_.

    DIM
        The type shall be _integer_.

RETURNS

Returns an array of same type and rank as the ARRAY argument.

EXAMPLES

Sample program:

    program demo_cshift
    implicit none
    integer, dimension(3,3) :: a
        a = reshape( [ 1, 2, 3, 4, 5, 6, 7, 8, 9 ], [ 3, 3 ])
        print '(3i3)', a(1,:)
        print '(3i3)', a(2,:)
        print '(3i3)', a(3,:)
        a = cshift(a, SHIFT=[1, 2, -1], DIM=2)
        print *
        print '(3i3)', a(1,:)
        print '(3i3)', a(2,:)
        print '(3i3)', a(3,:)
    end program demo_cshift

Results:

         1  4  7
         2  5  8
         3  6  9

         4  7  1
         8  2  5
         9  3  6

STANDARD

Fortran 95 and later

fortran-lang intrinsic descriptions
