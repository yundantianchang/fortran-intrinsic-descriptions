NAME

IACHAR(3) - [CHARACTER:CONVERSION] Code in ASCII collating sequence
(GFDL)

SYNTAX

    result = iachar(c, kind)

DESCRIPTION

IACHAR(c) returns the code for the ASCII character in the first
character position of C.

ARGUMENTS

    C
        Shall be a scalar _character_, with _intent(in)_

    KIND
        (Optional) An _integer_ initialization expression indicating the
        kind parameter of the result.

RETURNS

The return value is of type _integer_ and of kind KIND. If KIND is
absent, the return value is of default integer kind.

EXAMPLES

Sample program:

    program demo_iachar
    implicit none
    ! create function to convert uppercase letters to lowercase
       write(*,'(a)')lower('abcdefg ABCDEFG')
    contains
    !
    elemental pure function lower(str) result (string)
    ! Changes a string to lowercase
    character(*), intent(In)     :: str
    character(len(str))          :: string
    integer                      :: i
       string = str
       ! step thru each letter in the string in specified range
       do i = 1, len(str)
          select case (str(i:i))
          case ('A':'Z') ! change letter to miniscule
             string(i:i) = char(iachar(str(i:i))+32)
          case default
          end select
       end do
    end function lower
    !
    end program demo_iachar

Results:

       abcdefg abcdefg

NOTE

See ICHAR(3) for a discussion of converting between numerical values and
formatted string representations.

STANDARD

Fortran 95 and later, with KIND argument - Fortran 2003 and later

SEE ALSO

ACHAR(3), CHAR(3), ICHAR(3)

Functions that perform operations on character strings, return lengths
of arguments, and search for certain arguments:

-   ELEMENTAL: ADJUSTL(3), ADJUSTR(3), INDEX(3), SCAN(3), VERIFY(3)

-   NONELEMENTAL: LEN_TRIM(3), LEN(3), REPEAT(3), TRIM(3)

fortran-lang intrinsic descriptions
